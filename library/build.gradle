apply plugin: 'com.android.library'
//apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"



android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 3
        versionName version

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    resourcePrefix "gallery_"
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'io.reactivex.rxjava2:rxjava:2.1.0'
    api 'com.github.yalantis:ucrop:2.2.5'
    api 'androidx.recyclerview:recyclerview:1.0.0'
    api 'androidx.appcompat:appcompat:1.0.0'
    api 'androidx.exifinterface:exifinterface:1.0.0'
    api 'com.squareup.picasso:picasso:2.5.2'
    api 'com.facebook.fresco:fresco:1.3.0'
    api 'com.facebook.fresco:animated-gif:1.3.0'
    api 'com.github.bumptech.glide:glide:3.7.0'
    api 'com.mg.RxCustomizedImagePicker:fileprovider:1.0.0'
    api 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
}

//===================================upload jcenter=================================================
def siteUrl = 'https://github.com/liukefu2050/RxGalleryFinal'
def gitUrl = 'https://github.com/liukefu2050/RxGalleryFinal.git'

group = "com.mg.RxCustomizedImagePicker"
version = "1.0.2"
project.archivesBaseName = "library"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "https://androiddoc.qiniudn.com/reference/"
        title "RxCustomizedImagePicker 1.0.2"
    }
}

artifacts {
    archives sourcesJar
   // archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name "RxCustomizedImagePicker"
            description "android gallery"
            url siteUrl
            inceptionYear '2017'

            packaging 'aar'
            groupId "com.mg.RxCustomizedImagePicker"
            artifactId "library"
            version "1.0.2"
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id "liukefu2020"
                    name "liukefu2020"
                    email "liukefu2050@sina.com"
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }

        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "RxCustomizedImagePicker"
        userOrg = user
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = '1.0.2'
            desc = 'android gallery'
            released  = new Date()
            vcsTag = '1.0.2'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            gpg {
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}/**/

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

version = "1.1.3"

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 3
        versionName version
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    resourcePrefix "gallery_"
}



dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'io.reactivex.rxjava2:rxandroid:2.1.0'
    api 'io.reactivex.rxjava2:rxjava:2.2.1'
    api 'com.android.support:recyclerview-v7:27.1.1'
    api 'com.android.support:appcompat-v7:27.1.1'
    api 'com.android.support:exifinterface:27.1.1'
    api 'com.yalantis:ucrop:2.2.0'
    compileOnly 'com.squareup.picasso:picasso:2.71828'
    compileOnly 'com.facebook.fresco:fresco:1.10.0'
    compileOnly 'com.facebook.fresco:animated-gif:1.10.0'
    compileOnly 'com.github.bumptech.glide:glide:4.7.1'
    compileOnly 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
}

//===================================upload jcenter=================================================
def siteUrl = 'https://github.com/FinalTeam/RxGalleryFinal'
def gitUrl = 'https://github.com/FinalTeam/RxGalleryFinal.git'
group = "cn.finalteam.rxgalleryfinal"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'RxGalleryFinal'
                description = 'dujinyang desc'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dujinyang'
                        name 'dujinyang'
                        email '309933706@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "RxGalleryFinal"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
